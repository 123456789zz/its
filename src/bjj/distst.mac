	TITLE	DISTST
	SUBTTL	DISPLAY EXERCISER

	AC=	1
	AC2=	2
	AC1=	4
	CH=	3

	DIS=	10		;CHANNEL FOR DISPLAY

START:	RESET			;CLEAR EVERTHING
	OPEN	DIS,[15		;DUMP OUTPUT IS A MUST
	 	     SIXBIT	/DIS/	;ANY OLD DISPLAY WILL DO
		     0]		;NO BUFFERS

	  JRST	[OUTSTR	[ASCIZ	/DIS NOT AVAILABLE
TYPE CONTINUE TO RETRY/]
		 EXIT	1,	;WAIT FOR HIM TO GET SCOPE
		 JRST	.-1]	;RETRY OPEN

;WE HAVE SCOPE INITIALIZED, NOW FIND OUT WHAT HE WANTS TO SEE

GETCOM:	INCHRW	CH		;GET COMMAND INTO CH

	CAIL	CH,140		;CONVERT LOWER TO UPPER CASE
	  SUBI	CH,40
	CAIN	CH,"Z"		;EXIT COMMAND?
	JRST	EXIT1		;YES

	SUBI	CH,"A"		;CONVERT COMMAND TO A NUMBER FROM 0 TO MAX
	CAIL	CH,0		;MAKE SURE LEGAL
	CAILE	CH,DSPSIZ
	JRST	[OUTSTR	[ASCIZ	/?/]
		 JRST	GETCOM]

	JRST	DSPTAB(CH)	;COMMAND IS FINE, GO TO REQUESTED ROUTINE.

DSPTAB:	JRST	TST0
	JRST	TST1
	JRST	TST2
	JRST	TST3
	JRST	TST4
	JRST	TST5
	JRST	TST6
	JRST	TST7
	JRST	TST10
	DSPSIZ=.-DSPTAB

EXIT1:	RESET				;CLEAR ALL I/O
	EXIT	1,			;AND EXIT
	JRST	.-1			;NO CONTINUE ANYMORE


;FEED-FORWARD TEST (EXERCISE ALL FFD CONFIGURATIONS)
TST0:	OUTPUT	DIS,IOWD0	;START DISPLAY FOR DYNAMIC PICTURE

;DRAW DIAGANOLS ONLY IF DATA SWITCHE 0 IS ON
SWTCHK:	SWITCH	AC,		;DOES HE WANT DIAGONOLS?
	SETZM	DIAIOW		;ASSUME HE DON'T. (END IOWD LIST BEFORE DISPLAYING DIAG.)
	MOVE	AC2,[IOWD	5,DIAG]	;BUT BE READY IF HE DOES
	TLNE	AC,400000	;DOES HE?
	MOVEM	AC2,DIAIOW	;YES HE DOES, PUT IN IOWD FOR DIAGONOLS
	MOVEI	AC2,1		;GET SOME SLEEP(SO AS NOT TO TAKE TOO MUCH TIME)
	SLEEP	AC2,
	SKPINC			;GOT ANOTHER COMMAND FOR US?
	JRST	SWTCHK		;NOPE, CONTINUE DISPLAYING WHAT WE HAAVE BEEN
	JRST	GETCOM		;YEP, SEE WHAT HE WANTS

IOWD0:	IOWD	^D32,DISWRD	;BOXES AND POINTS
DIAIOW:	IOWD	^D5,DIAG	;DIAGONOLS
	IOWD	^D5,DIAG	;AGAIN
	XWD	0,0		;NEVER MORE

;		DISPLAY INSTRUCTIONS FOR BOXES AND POINTS
DISWRD:
020177,,220000	;MODE=PARAMETER       SCALE=3 INTENSITY=7
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
022000,,223777	;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
		;MODE=POINT           Y CO-ORDINATE DISPLAY ADDRESS=1777
023777,,222000	;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1777
		;MODE=POINT           Y CO-ORDINATE DISPLAY ADDRESS=0000
221000,,023000	;MODE=POINT           Y CO-ORDINATE ADDRESS=1000
		;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1000
220010,,100010	;MODE=POINT           Y CO-ORDINATE ADDRESS=0010
		;MODE=POINT           X CO-ORDINATE ADDRESS=0010
200176,,277000	;MODE=VECTOR          DISPLAY X LENTH=176 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=176
200376,,777000	;MODE=VECTOR          DISPLAY X LENTH=-176 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-176
020140,,220404	;MODE=PARAMETER       SCALE=2
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0404
100404,,200176	;MODE=POINT           X CO-ORDINATE ADDRESS=0404
		;MODE=VECTOR          DISPLAY X LENTH=176 Y LENTH=000
277000,,200376	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=176
		;MODE=VECTOR          DISPLAY X LENTH=-176 Y LENTH=000
777000,,020120	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-176
		;MODE=PARAMETER       SCALE=1
220602,,100602	;MODE=POINT           Y CO-ORDINATE ADDRESS=0602
		;MODE=POINT           X CO-ORDINATE ADDRESS=0602
200176,,277000	;MODE=VECTOR          DISPLAY X LENTH=176 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=176
200376,,777000	;MODE=VECTOR          DISPLAY X LENTH=-176 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-176
020100,,220701	;MODE=PARAMETER       SCALE=0
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0701
100701,,200176	;MODE=POINT           X CO-ORDINATE ADDRESS=0701
		;MODE=VECTOR          DISPLAY X LENTH=176 Y LENTH=000
277000,,200376	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=176
		;MODE=VECTOR          DISPLAY X LENTH=-176 Y LENTH=000
777000,,020140	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-176
		;MODE=PARAMETER       SCALE=2
220100,,100100	;MODE=POINT           Y CO-ORDINATE ADDRESS=0100
		;MODE=POINT           X CO-ORDINATE ADDRESS=0100
200100,,240000	;MODE=VECTOR          DISPLAY X LENTH=100 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=100
200300,,740000	;MODE=VECTOR          DISPLAY X LENTH=-100 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-100
020000,,220100	;MODE=PARAMETER
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0100
101300,,200100	;MODE=POINT           X CO-ORDINATE ADDRESS=1300
		;MODE=VECTOR          DISPLAY X LENTH=100 Y LENTH=000
240000,,200300	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=100
		;MODE=VECTOR          DISPLAY X LENTH=-100 Y LENTH=000
740000,,020000	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-100
		;MODE=PARAMETER
221300,,101300	;MODE=POINT           Y CO-ORDINATE ADDRESS=1300
		;MODE=POINT           X CO-ORDINATE ADDRESS=1300
200100,,240000	;MODE=VECTOR          DISPLAY X LENTH=100 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=100
200300,,740000	;MODE=VECTOR          DISPLAY X LENTH=-100 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-100
020000,,221300	;MODE=PARAMETER
		;MODE=POINT           Y CO-ORDINATE ADDRESS=1300
100100,,200100	;MODE=POINT           X CO-ORDINATE ADDRESS=0100
		;MODE=VECTOR          DISPLAY X LENTH=100 Y LENTH=000
240000,,200300	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=100
		;MODE=VECTOR          DISPLAY X LENTH=-100 Y LENTH=000
740000,,000000	;MODE=VECTOR          DISPLAY X LENTH=000 Y LENTH=-100
		;MODE=PARAMETER

;		DISPLAY INSTRUCTIONS FOR DIAGONOLS
DIAG:
020115,,221777	;MODE=PARAMETER       SCALE=0 INTENSITY=5
		;MODE=POINT           Y CO-ORDINATE ADDRESS=1777
120000,,377577	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR CONTINUE DISPLAY X LENTH=177 Y LENTH=-177
020000,,220000	;MODE=PARAMETER
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
120000,,277577	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR CONTINUE DISPLAY X LENTH=177 Y LENTH=177
000300,,000000	;MODE=PARAMETER       SCALE=0
		;MODE=PARAMETER

;D/A TEST (TO ALLOW ACCURATE COMPARISON OF THE VOLTAGES DEVELOPED IN
;EACH BIT OF THE A/D CONVERTERS)
TST1:	OUTPUT	DIS,IOWD1

SWCHK1:	SWITCH	AC,		;GET X AND Y ADDRESSES
	AND	AC,[777,,777]	;MASK OUT UNWANTED BITS
	IOR	AC,[220000,,22000];MAKE INTO 2 POINT INSTRUCTIONS
	MOVEM	AC,PNTAD1	;STORE NEW INSTRUCTION FOR DISPLAY
	AOBJP	AC,.+1		;CHANGE TO POINT 1 GREATER IN BOTH DIRECTIONS
	TRZ	AC,20000	;NEXT INSTRUCTION WILL BE PARAMETER
	MOVEM	AC,PNTAD1+1	;STORE THIS INSTRUCTION AFTER LAST

	MOVEI	AC,1		;SLEEP FOR 1 SECONT
	SLEEP	AC,		;DON'T WASTE CPU TIME

	SKPINC			;GOT ANOTHER COMMAND?
	JRST	SWCHK1		;NO, CONTINUE WITH WHAT WE ARE DOING
	JRST	GETCOM		;YES, GO DISPLAY IT

IOWD1:	IOWD	3,DISWR1	;3 WORDS AT DISWR1 TO BE DISPLAYED
	XWD	0,0

DISWR1:	20114
PNTAD1:	220000,,22000
	220000,,20000


;5 POINTS
TST2:	OUTPUT	DIS,IOWD2		;DISPLAY 5 POINTS
	JRST	GETCOM			;AND GET NEXT COMMAND

IOWD2:	IOWD	4,DISWR2
	XWD	0,0

DISWR2:
XWD	020116,220000	;MODE=PARAMETER      SCALE=0 INTENSITY=6
			;MODE=POINT         Y CO-ORDINATE ADDRESS=0000
XWD	022000,023777	;MODE=PARAMETER      X CO-ORDINATE DISPLAY ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1777
XWD	223777,022000	;MODE=POINT           Y CO-ORDINATE DISPLAY ADDRESS=1777
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
XWD	221000,003000	;MODE=POINT           Y CO-ORDINATE ADDRESS=1000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1000


;ARBITRARY POINT
TST3:	OUTPUT	DIS,IOWD3		;START DISPLAY
SWCHK3:	SWITCH	AC,			;GET X AND Y CO-ORDINATES FOR POINT
	AND	AC,[1777,,1777]		;MASK UNWANTED BITS
	IOR	AC,[220000,,2000]	;MAKE INTO POINT INSTRUCTIONS
	MOVEM	AC,DSPNT3		;STORE FOR DISPLAY

	MOVEI	AC,1			;SLEEP FOR 1 SECOND
	SLEEP	AC,
	SKPINC				;ANOTHER COMMAND WAITING?
	JRST	SWCHK3			;DISPLAY  POINT REQUESTED NOW
	JRST	GETCOM			;YES, GO GET IT

IOWD3:	IOWD	2,DISWR3
	XWD	0,0

DISWR3:
XWD	0,20114
DSPNT3:	Z


;VECTOR PATTERN
TST4:	OUTPUT	DIS,IOWD4		;DISPLAY SQUARE ON END
	JRST	GETCOM			;AND WAIT FOR NEXT COMMAND

IOWD4:	IOWD	4,DISWR4
	XWD	0,0

DISWR4:
XWD	020112,221000	;MODE=PARAMETER       SCALE=0 INTENSITY=2
			;MODE=POINT           Y CO-ORDINATE ADDRESS=1000
XWD	103000,277577	;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1000
			;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=177
XWD	277777,377777	;MODE=VECTOR          DISPLAY X LENTH=-177 Y LENTH=177
			;MODE=VECTOR          DISPLAY X LENTH=-177 Y LENTH=-177
XWD	777577,0	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=-177


;INCREMENT PATTERN
TST5:	OUTPUT	DIS,IOWD5		;START DISPLAY

SWCHK5:	SWITCH	AC,			;GET PARAMETER INFO
	ANDI	AC,67			;MASK UNUSED BITS
	IORI	AC,20110		;MAKE INTO PARAM INSTRUCTION
	MOVEM	AC,DISWR5		;STORE FOR DISPLAY
	MOVEI	AC,1			;SLEEP FOR 1 SECOND
	SLEEP	AC,
	SKPINC				;GOT ANOTHER COMMAND?
	JRST	SWCHK5			;NO, GET NEXT PARAM INSTRUCTION
	JRST	GETCOM			;YES, GET COMMAND

IOWD5:	IOWD	4,DISWR5
	XWD	0,0

DISWR5:
XWD	000000,020000	;MODE=PARAMETER
			;MODE=PARAMETER
XWD	220777,142777	;MODE=POINT           Y CO-ORDINATE ADDRESS=0777
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0777
XWD	221042,221377	;MODE=INCREMENT
			;MODE=INCREMENT
XWD	774210,000000	;MODE=INCREMENT
			;MODE=INCREMENT


;REPEATABILITY TEST
TST6:	OUTPUT	DIS,IOWD6

SWCHK6:	SWITCH	AC,
	AND	AC,[1777,,1777]		;MASK ALL BUT X AND Y CO-ORDINATES
	IOR	AC,[220000,,22000]	;MAKE INTO POINT INSTRUCTIONS
	MOVEM	AC,DSPNT6		;STORE FOR DISPLAY
	MOVEM	AC,DSPNT6+2		;AND DISPLAY HERE TOO
	MOVEM	AC,DSPNT6+4		;AND HERE TOO
	TRZ	AC,20000		;LAST POINT INSTRUCTION, FOLLOWED BY PARAMETER
	MOVEM	AC,DSPNT6+6

	MOVEI	AC,1			;SLEEP FOR 1 SECOND
	SLEEP	AC,
	SKPINC				;ANOTHER COMMAND ?
	JRST	SWCHK6			;NO, CHECK SWITCHES AGAIN
	JRST	GETCOM			;GET NEXT COMMAND

IOWD6:	IOWD	^D9,DISWR6
	XWD	0,0

DISWR6:
XWD	000000,020114	;MODE=PARAMETER
			;MODE=PARAMETER       SCALE=0 INTENSITY=4
XWD	220000,022000	;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
DSPNT6:
XWD	220000,022000	;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
XWD	220000,023777	;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1777
XWD	220000,022000	;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
XWD	221777,023777	;MODE=POINT           Y CO-ORDINATE ADDRESS=1777
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=1777
XWD	220000,022000	;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
XWD	221777,022000	;MODE=POINT           Y CO-ORDINATE ADDRESS=1777
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000
XWD	220000,022000	;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
			;MODE=POINT           X CO-ORDINATE DISPLAY ADDRESS=0000


;LIGHT PEN SENSITIVITY TEST
	LPFLG=2000
	DPY=100
	DISPI=56
TST7:	OUTPUT	DIS,IOWD7	;START DISPLAYING LINES

	MOVEI	AC,1		;LOCK LOW SEGMENT
	LOCK	AC,		;SO WE CAN USE REAL TIME PROGRAMMING
	  JRST	[OUTSTR	[ASCIZ	/
%UNABLE TO LOCK IN CORE
/]
		 JRST	GETCOM]
	HRRZS	AC		;MAKE SURE ONLY LOW SEGMENT ADDRESS
	LSH	AC,9		;JUSTIFY ADDRESS
	MOVEM	AC,ABSADR#	;STORE FOR FUTURE REFERENCE
	JSR	RELOCT		;RELOCATE INTERUPT LEVEL PROGRAM
	MOVEI	AC,RTBLK	;CONNECT REAL TIME ROUTINE
	RTTRP	AC,		;TO THE PI SYSTEM
	  JRST	[OUTSTR	[ASCIZ	/
%UNABLE TO EXECUTE RTTRP UUO
/]
		 JRST	START]	;UNLOCK US AND RESTART
				;OUR WORK IS DONE, LET INTERUPT ROUTINE DO THE REST
	MOVEI	AC,1		;SLEEP UNTIL ANOTHER COMMAND IS TYPED
	SLEEP	AC,		;..
	SKPINC			;DO I HAVE TO WAKE UP?
	JRST	.-3		;NO, BAKC TO SLEEP
	RESET			;YES, NO MORE INTERUPTS PLZ
	MOVN	AC,ABSADR	;RESTORE RELOCATED ADDRESSES
	JSR	RELOCT		;..
	JRST	START		;GET NEXT COMMAND

RELOCT:	Z			;JSR HERE TO RELOCATE INTERUPT ROUTINE USING VALUE IN AC
	ADDM	AC,REL1
	ADDM	AC,REL2
	ADDM	AC,REL3
	ADDM	AC,REL4
	ADDM	AC,REL5
	JRST	@RELOCT

RTBLK:	XWD	-5,TRPADR	;PI CHANNEL=5(DO NOT REMOVE OTHER OCCURENCES OF DIS)
	XWD	1,APRTRP	;EXEC MODE,,APR ERRORS TO APRTRP
	CONSO	130,LPFLG	;TRAP IF LIGHT PEN
	0			;NO BLKI/BLKO INSTRUCTION

TRPADR:	Z
REL1:	MOVEM	ACSAVE#		;SAVE AN ACCUMULATOR
	DATAI	130,		;GET LP ADDRESS
	TRNN	1000		;X>777?
REL2:	JRST	.+3		;NO, CONTINUE PICTURE
	HRROS	@54		;SET LEFT HALF OF SCOPE BLKO INSTRUCTION TO -1
	CONO	130,DPY		;TELL SCOPE IT'S DONE(NOW FINISHED WITH PICTURE)
	CONO	130,DISPI	;CLEAR LP FLAG
REL3:	MOVE	ACSAVE		;RESTORE ACCUMULATOR
REL4:	JRSTF	@TRPADR		;DISMISS INTERUPT

APRTRP:	Z			;APR ERRORS HERE
REL5:	JRST	REL3		;NOTHING I CAN DO ABOUT IT.

IOWD7:	IOWD	DSSIZ7,DISWR7
	XWD	0,0

DISWR7:
034117,,221600	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=7
		;MODE=POINT           Y CO-ORDINATE ADDRESS=1600
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034116,,221400	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=6
		;MODE=POINT           Y CO-ORDINATE ADDRESS=1400
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034115,,221200	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=5
		;MODE=POINT           Y CO-ORDINATE ADDRESS=1200
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034114,,221000	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=4
		;MODE=POINT           Y CO-ORDINATE ADDRESS=1000
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034113,,220600	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=3
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0600
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034112,,220400	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=2
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0400
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034111,,220200	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=1
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0200
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
034110,,220000	;MODE=PARAMETER       LP=1 SCALE=0 INTENSITY=0
		;MODE=POINT           Y CO-ORDINATE ADDRESS=0000
100000,,200177	;MODE=POINT           X CO-ORDINATE ADDRESS=0000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
200177,,200177	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
600177,,000000	;MODE=VECTOR          DISPLAY X LENTH=177 Y LENTH=000
		;MODE=PARAMETER
	DSSIZ7=.-DISWR7


;RIPPLE TEST
TST10:	OUTPUT	DIS,IOWD10

SWCH10:	SWITCH	AC1,
	TRZ	AC1,776000
	HRRZM	AC1,AC
	HRLZI	0,1
	TLNE	AC1,400000
	JRST	.+3
	MOVSS	0
	MOVSS	AC
	TLO	AC,220000
	IORI	AC,2000
	MOVEM	AC,DSVC10
	HRRZI	AC2,1777
	MOVEI	5,10
	HIBER	5,
	ADDM	0,DSVC10
	SOJG	AC2,.-3
	MOVEI	AC,1
	SLEEP	AC,
	SKPINC
	JRST	SWCH10
	JRST	GETCOM

IOWD10:	IOWD	2,DSWR10
	XWD	0,0

DSWR10:
	XWD	0,30017
DSVC10:	XWD	0,0

	END	START
