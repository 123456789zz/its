TITLE DEUNA - poke at a DEUNA

.insrt system; ksdefs >

a=1
b=2
c=3
p=17

.insrt system; dedefs >

go:
	move p,[-100,,pdl]

	seto a,			;Enable user I/O
	.iotlsr a,

	movei a,%drset		;Reset DEUNA
	iowri a,%decr0
	pushj p,wait

	movei a,%dinte		;Enable interrupt
	iowri a,%decr0

	setz a,			;Clear PCSR2 and 3
	iowri a,%decr2
	iowri a,%decr3

	movei a,%dinte+%dgpcb	;Command Get PCB block
	iowri a,%decr0		;Expect an interrupt

	.call [setz		;Get a locked page.
		sixbit /CORBLK/
		movei %cbndr ;+%cblok
		movei %jself ? movei page
		movei 400000 ? setzi 100] ;%jsnew
	 .lose
done:	jfcl
	.call [setz
		sixbit /CORTYP/
		movei %jself ? movei page
		movem a ? movem b ? setzm c]
	 .lose

	.logout 1,

wait:	iordi a,%decr0		;Wait for interrupt bit
	trnn a,%dintr
	 jrst wait
	popj p,

pdl:	block 100

page==<.+1777>_-12

end go
