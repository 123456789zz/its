;;; LIBRARY OF LOGO FUNCTIONS FOR LLOGO.
;;; IT IS MOSTLY HIGHER ORDER FUNCTION DEFINITIONS,
;;; WRITTEN BY BRIAN HARVEY AND
;;; PORTED TO ITS LLOGO BY ALEXEY SLYUSAR

;; version 0.01

TO ?
10 OP :TEMPLATE.VAR
END

TO ?REST
10 OP BF :TEMPLATE.LIST
END

TO PREPARE.TEMPLATE :TEMPLATE
10 IF LISTP :TEMPLATE OP :TEMPLATE
20 OP SE :TEMPLATE '?
END

TO PREPARE.TEMPLATE.2 :TEMPLATE
10 IF LISTP :TEMPLATE OP :TEMPLATE
20 OP SE :TEMPLATE [?1 ?2]
END

TO ?1
10 OP :TEMPLATE.VAR1
END

TO ?2
10 OP :TEMPLATE.VAR2
END

TO FILTER :FILTER.TEMPLATE :TEMPLATE.LIST
10 OP FILTER1 PREPARE.TEMPLATE :FILTER.TEMPLATE :TEMPLATE.LIST 1
END

TO FILTER1 :FILTER.TEMPLATE :TEMPLATE.LIST :TEMPLATE.NUMBER
10 IF EMPTYP :TEMPLATE.LIST OP []
20 LOCAL 'TEMPLATE.VAR
30 MAKE 'TEMPLATE.VAR FIRST :TEMPLATE.LIST
40 IF (RUN :FILTER.TEMPLATE) OP FPUT :TEMPLATE.VAR FILTER1 :FILTER.TEMPLATE BF :TEMPLATE.LIST 1 + :TEMPLATE.NUMBER
50 OP FILTER1 :FILTER.TEMPLATE BF :TEMPLATE.LIST 1 + :TEMPLATE.NUMBER
END

TO REDUCE :REDUCE.TEMPLATE :TEMPLATE.LIST
10 OP REDUCE1 PREPARE.TEMPLATE.2 :REDUCE.TEMPLATE :TEMPLATE.LIST
END

TO REDUCE1 :REDUCE.TEMPLATE :TEMPLATE.LIST
10 IF EMPTYP BUTFIRST :TEMPLATE.LIST OP FIRST :TEMPLATE.LIST
20 LOCAL 'TEMPLATE.VAR1
30 LOCAL 'TEMPLATE.VAR2
40 MAKE 'TEMPLATE.VAR1 FIRST :TEMPLATE.LIST
50 MAKE 'TEMPLATE.VAR2 REDUCE1 :REDUCE.TEMPLATE BF :TEMPLATE.LIST
60 OP RUN :REDUCE.TEMPLATE
END

TO FIND :FIND.TEMPLATE :TEMPLATE.LIST
10 IF EMPTYP :TEMPLATE.LIST OP []
20 OP FIND1 FIRST :TEMPLATE.LIST
END

TO FIND1 :TEMPLATE.VAR
10 IF RUN :FIND.TEMPLATE OP FIRST :TEMPLATE.LIST
20 OP FIND :FIND.TEMPLATE BF :TEMPLATE.LIST
END

TO FOREACH :TEMPLATE.LIST :FOREACH.TEMPLATE
10 IF EMPTYP :TEMPLATE.LIST STOP
20 FOREACH1 FIRST :TEMPLATE.LIST
END

TO FOREACH1 :TEMPLATE.VAR
10 RUN :FOREACH.TEMPLATE
20 FOREACH BF :TEMPLATE.LIST :FOREACH.TEMPLATE
END

ERASE PRIMITIVE MAP
; MAP ERASED
TO MAP :MAP.TEMPLATE :TEMPLATE.LIST
10 OP MAP1 PREPARE.TEMPLATE :MAP.TEMPLATE :TEMPLATE.LIST 1
END
;MAP DEFINED

TO MAP1 :MAP.TEMPLATE :TEMPLATE.LIST :TEMPLATE.NUMBER
10 IF EMPTYP :TEMPLATE.LIST OP []
20 LOCAL 'TEMPLATE.VAR
30 MAKE 'TEMPLATE.VAR FIRST :TEMPLATE.LIST
40 OP FPUT (RUN :MAP.TEMPLATE) (MAP1 :MAP.TEMPLATE (BF :TEMPLATE.LIST) 1+:TEMPL!
ATE.NUMBER)
END
;MAP1 DEFINED

TO MAP.SE :MAP.SE.TEMPLATE :MAP.SE.LIST
10 IF EMPTYP :MAP.SE.LIST OP []
20 OP MAP.SE1 FIRST :MAP.SE.LIST
END
;MAP.SE DEFINED

TO MAP.SE1 :TEMPLATE.VAR
10 OP SE (RUN :MAP.SE.TEMPLATE)(MAP.SE :MAP.SE.TEMPLATE BF :MAP.SE.LIST)
END
;MAP.SE1 DEFINED

TO ITEM :ELEMENT :LIST
10 IF :ELEMENT = 1 OP FIRST :LIST
20 OP ITEM (:ELEMENT - 1) BF :LIST
END
;ITEM DEFINED

ERASE PRIMITIVE MEMBER
; MEMBER ERASED
TO MEMBERP :WORD :LIST
10 IF EMPTYP :LIST OP 'NIL
20 IF EQUAL :WORD (FIRST :LIST) OP 'T
30 OP MEMBERP :WORD (BF :LIST)
END
;MEMBER DEFINED
